/*
 * This is A MODEL, result of problem analysis 
 * written in the CUSTOM meta-model/language qa
 */  
System bls0  
Dispatch turn     : switch   
Event local_click : clicked(N)  //N : natural

Context bls0Ctx ip [ host="localhost"  port=8029 ]  -g cyan  

QActor qaled context bls0Ctx {
	Plan init normal  [         
   		println( qaled(starts)  )  ;
   		actorOp createLedPojo 
   	]  
   	switchTo waitForCmd 
   	 
  	Plan waitForCmd [  
  		println("LED WAITS")
  	]
    transition stopAfter 3000000
    	whenMsg turn : switch do actorOp ledSwitch
    finally repeatPlan	
}

QActor ledtester context bls0Ctx   {  
 	Plan init normal [     
   		delay 1000 ;
 		forward qaled -m turn : switch ;
 		delay 500 ;
 		forward qaled -m turn : switch  
	]
 	finally	repeatPlan 2	
}
 
QActor qacontrol context bls0Ctx{
	Plan init normal [         
   		println( qacontrol(starts)  )  ;
   		actorOp createAButtonPojo
   	]
   	switchTo waitForClick      
 
    Plan waitForClick[
     	actorOp noOP   
    ]  
    transition stopAfter 100000 
     	whenEvent local_click : clicked(N) do forward qaled -m turn : switch
    finally repeatPlan	
}
     

