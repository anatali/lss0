/*
 * blsHlBlinkReactive.qa
 */  
System blsHlBlinkReactive 
Dispatch turn     		: turn(X)   
Event local_click 		: clicked(N)  	//N : natural
Event usercmd     		: usercmd(N)
Event local_ledstate    : ledstate(V)	//V=ready/blinking

Context ctxBlsHlBlimkreactive ip [ host="localhost"  port=8059 ]  -httpserver   
//EventHandler evconvert for usercmd -print {
//	emit local_click fromContent usercmd (N) to clicked(N) 
//};   

QActor qaledhlreactive context ctxBlsHlBlimkreactive  {
	Plan init normal  [         
 		javaOp "it.unibo.custom.led.LedFactory.createLedWithGui(\"l1\", this)"
    ]
   	switchTo waitForCmd   
   	  
  	Plan waitForCmd [  
  		println("qaledhlreactive WAITS")
  	]
    transition stopAfter 3000000
     	whenMsg turn -> doLedCmd
    finally repeatPlan	
    
    Plan doLedCmd [
    	onMsg turn : turn(blink)  -> switchTo doLedBlink
    ]
    
    Plan doLedBlink[ ]
    reactive javaOp "it.unibo.custom.led.LedFactory.ledBlink(\"l1\" )"
   		whenEnd               -> endWork
   		whenTout 30000        -> endWork    		
   		whenEvent usercmd     -> stopBlink 
   	
   	Plan stopBlink[
     	actorOp suspendCurrentAction;
    	emit local_ledstate : ledstate(ready)  //to review the button
    ]
    switchTo waitForCmd
   	
   	Plan endWork[ println("qaledhlreactive ends") ]
}
  
/*
 * CONTROL
 * Converts a click of the button into a message for the led to blink
 * WARNING: each message is STORED => we hide the button from the view!!!
 */  
QActor qacontrolhlblinkreactive context ctxBlsHlBlimkreactive {
	Plan init normal [         
   		actorOp createButtonObject("gui", "start") 		//must emit local_click
   	]
   	switchTo waitForClick       
 
    Plan waitForClick[
     	println("qacontrolhlblink waitForClick") ;
    	actorOp showTheButton 
     ]  
    transition stopAfter 100000 
     	whenEvent local_click -> activateTheLed 
    finally repeatPlan  
    
    Plan activateTheLed resumeLastPlan[
    	actorOp hideTheButton ;
    	forward qaledhlreactive -m turn : turn(blink)
    ]
    transition stopAfter 3000000
     	whenEvent local_ledstate : ledstate(ready) do switchTo waitForClick
 }
     
